/*
 * Copyright © 2023, 2024 Damyan Ivanov.
 * This file is part of MoLe.
 * MoLe is free software: you can distribute it and/or modify it
 * under the term of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your opinion), any later version.
 *
 * MoLe is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License terms for details.
 *
 * You should have received a copy of the GNU General Public License
 * along with MoLe. If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    // KSP available but disabled during Java→Kotlin migration.
    // Enable after DAO conversion: id 'com.google.devtools.ksp' version '1.9.25-1.0.20'
}

// Load keystore properties for release signing
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 34
    defaultConfig {
        applicationId "net.ktnx.mobileledger"
        minSdkVersion 22
        targetSdkVersion 34
        vectorDrawables.useSupportLibrary true
        versionCode 59
        versionName '0.22.1'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }

        // KSP Room configuration (disabled during migration)
        // ksp {
        //     arg("room.schemaLocation", "$projectDir/schemas")
        //     arg("room.incremental", "true")
        //     arg("room.expandProjection", "true")
        // }
    }
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
        }
        pre {
            applicationIdSuffix '.pre'
            versionNameSuffix '-pre'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets', 'src/main/assets/']
            java.srcDirs += 'src/main/kotlin'
        }
        test {
            java.srcDirs += 'src/test/kotlin'
        }
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xjsr305=strict"]
    }
    productFlavors {
    }
    buildFeatures.viewBinding = true
    buildToolsVersion '34.0.0'
    namespace 'net.ktnx.mobileledger'
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.25"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

    // AndroidX Lifecycle
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    // Room
    // Note: Using annotationProcessor during Java→Kotlin migration for compatibility.
    // Will switch to KSP once all DAOs are converted to Kotlin.
    def room_version = '2.4.2'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Navigation
    def nav_version = '2.4.2'
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // Jackson with Kotlin module
    implementation 'com.fasterxml.jackson.module:jackson-modules-java8:2.17.1'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.17.1'

    // AndroidX
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Annotations
    implementation 'org.jetbrains:annotations:24.1.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0-alpha05'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs <<
                    "-Xlint:deprecation" <<
                    "-Xlint:unchecked"
        }
    }
}
